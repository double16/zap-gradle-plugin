/**
 * Copyright (c) 2018-2019, Patrick Double. All right reserved.
 *
 * Released under BSD-3 style license.
 * See http://opensource.org/licenses/BSD-3-Clause
 */

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'ru.vyarus.quality' version '4.0.0'
    id 'com.bmuschko.clover' version '2.2.4'
    id 'org.sonarqube' version '2.8'
}

apply plugin: 'idea'

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'org.zaproxy:zap-clientapi:1.8.0'
    implementation 'de.undercouch:gradle-download-task:4.0.2'
    testImplementation 'junit:junit:4.13'
    testImplementation gradleApi()
    clover 'org.openclover:clover:4.4.1'
}

description = "Automate web application dynamic security scans using OWASP ZAP"
group = 'com.patdouble'
version = '2.0-SNAPSHOT'

// Set product version if tag is given on CI
System.getenv('CIRCLE_TAG')?.eachMatch(/^[0-9]+(\.[0-9]+)*$/) { m -> project.version = m[0] }

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceUrl = 'yes'
}

pluginBundle {
    website = 'https://github.com/double16/zap-gradle-plugin'
    vcsUrl = 'https://github.com/double16/zap-gradle-plugin.git'
    description = project.description
    tags = ['security', 'devsecops']

    plugins {
        zapPlugin {
            id = 'com.patdouble.zap'
            displayName = project.description
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId rootProject.group
            artifactId rootProject.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name rootProject.name
                    description rootProject.description
                }
            }
        }
    }
}

task install(dependsOn: 'publishToMavenLocal')

// Set product version if tag is given on CI
System.getenv('CIRCLE_TAG')?.eachMatch(/^[0-9]+(\.[0-9]+)*$/) { m -> project.version = m[0] }

tasks.publishPlugins.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

quality {
    strict = false
}

clover {
    report {
        xml = true
        html = true
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', System.getenv('SONARQUBE_URL') ?: 'https://sonarcloud.io'
        property 'sonar.organization', System.getenv('SONARQUBE_ORG')
        property 'sonar.login', System.getenv('SONARQUBE_LOGIN')
        property 'sonar.clover.reportPath', "${cloverGenerateReport.reportsDir.absolutePath}/clover/clover.xml"
    }
}
tasks.sonarqube.dependsOn check, cloverGenerateReport

